;+
; Copyright Â© 2016, United States Government as represented by the
; Administrator for The National Aeronautics and Space Administration.
; All Rights Reserved.
;
; DESCRIPTION:
;       Contains two modules used to produce a multiplot with scatter plots of
;       PR and GV data, one for each breakout of the data by rain type and
;       surface type.  Output is to the display.
;
;       Primary Module:  plot_scatter_rr_vs_tbb_by_raintype_ps, title, pr_rr_in, $
;                            gv_rr_in, Tbb_in, raintype_in, $
;                            UNITS=units, Y_TITLE=y_title, X_TITLE=x_title
;       Internal Module: plot_rr_tbb_scat_ps, pos, sub_title, siteID, ncolor, $
;                            charsz, xdata, ydata, ndata, UNITS=units, $
;                            Y_TITLE=y_title_in, X_TITLE=x_title_in
;
; HISTORY:
;       Bob Morris/GPM GV (SAIC), August, 2016
;       - Created from plot_scatter_by_sfc_type3x3_ps.pro.
;
;
; EMAIL QUESTIONS OR COMMENTS TO:
;       <Bob Morris> kenneth.r.morris@nasa.gov
;       <Matt Schwaller> mathew.r.schwaller@nasa.gov
;-
;------------------------------------------------------------------------------

pro plot_rr_tbb_scat_ps, pos, sub_title, siteID, ncolor, charsz, xdata, ydata, $
                      ndata, SFCCOLOR=sfcColor, UNITS=units, $
                      Y_TITLE=y_title_in, X_TITLE=x_title_in

ncolor=255
IF (N_PARAMS() EQ 8) THEN symsize=(0.65/( alog10(ndata) > 0.1 ) > 0.25) < 0.7 $
ELSE symsize=0.5

IF N_ELEMENTS(units) NE 1 THEN units = 'mm/h'
IF N_ELEMENTS(x_title_in) NE 1 THEN x_title = 'GMI 89v Tbb (K)' $
   ELSE x_title = x_title_in
IF N_ELEMENTS(y_title_in) NE 1 THEN y_title = 'GMI rainate, mm/h' $
   ELSE y_title = y_title_in

yminRR = 0.1
ymaxRR = 100.0
xminTbb = 90.0
xmaxTbb = 300.0

IF ( N_PARAMS() GT 5 ) THEN BEGIN

   ; --- Define symbol as filled circle
   A = FINDGEN(17) * (!PI*2/16.)
   USERSYM, COS(A), SIN(A), /FILL

   IF N_ELEMENTS(sfcColor) EQ 0 THEN BEGIN
      plot, POSITION=pos, xdata[0:ndata-1], ydata[0:ndata-1], $
         xticklen=+0.04, yticklen=0.04, /noerase, $
         xrange=[xminTbb,xmaxTbb],yrange=[yminRR,ymaxRR], /ylog, $
         xticks=7, yticks=5, xminor=3, yminor=5, xtickinterval=30, $
         title=sub_title, ytitle=y_title, xtitle=x_title, $
         color=ncolor,charsize=0.7, psym=8, symsize=symsize
   ENDIF ELSE BEGIN
      plot, POSITION=pos, [xminTbb,xmaxTbb], [yminRR,ymaxRR], /nodata, $
         xticklen=+0.04, yticklen=0.04,/noerase, $
         xrange=[xminTbb,xmaxTbb],yrange=[yminRR,ymaxRR], $
         /ylog, xticks=7, yticks=5, xminor=3, yminor=5, xtickinterval=30, $
         title=sub_title, ytitle=y_title, xtitle=x_title, $
         color=ncolor,charsize=0.7

      plots, xdata[0:ndata-1], ydata[0:ndata-1], $
         color=sfcColor, psym=8, symsize=symsize
   ENDELSE

    ; plot lines of +/- 1, 5, and 25 mm/hr error
;     FOR loc = xymin, xymax DO BEGIN
;        FOR expon = 0,2 DO BEGIN
;           oplot,[loc+errlimit^expon,loc+errlimit^expon+1],[loc,loc+1],color=ncolor, LINESTYLE=2
;           oplot,[loc,loc+1],[loc+errlimit^expon,loc+errlimit^expon+1],color=ncolor, LINESTYLE=2
;        ENDFOR
;     ENDFOR

;   oplot,[xminTbb,xmaxTbb],[yminRR,ymaxRR],color=ncolor

   IF ndata GT 4 THEN BEGIN
      correlation = correlate(xdata[0:ndata-1],ydata[0:ndata-1])

      xyouts,pos[0]+0.018, pos[3]-0.02, "Correlation = "+string(correlation, $
            format='(f4.2)')+"", $
            charsize=0.65,color=ncolor,alignment=0,/normal

      standard_error, xdata[0:ndata-1],ydata[0:ndata-1], STD_ERROR=std_error

      xyouts,pos[0]+0.018, pos[3]-0.033, "Std. error = "+string(std_error, $
            format='(f4.2)')+"", $
            charsize=0.65,color=ncolor,alignment=0,/normal

   ENDIF

   if ndata ge 0 and ndata lt 10 then fmt='(i1)'
   if ndata ge 10 and ndata lt 100 then fmt='(i2)'
   if ndata ge 100 and ndata lt 1000 then fmt='(i3)'
   if ndata ge 1000 and ndata lt 10000 then fmt='(i4)'
   if ndata ge 10000 and ndata lt 100000 then fmt='(i5)'

   xyouts,pos[0]+0.018, pos[3]-0.046, "Points = "+string(ndata, $
         format=fmt)+"", charsize=0.65,color=ncolor,alignment=0,/normal
            
ENDIF ELSE BEGIN
  ; print, 'Plotting empty panel for ', sub_title
   plot, POSITION=pos, [xminTbb,xmaxTbb], [yminRR,ymaxRR], /nodata, $
         xticklen=+0.04, yticklen=0.04,/noerase, $
         xrange=[xminTbb,xmaxTbb],yrange=[yminRR,ymaxRR], $
         /ylog, xticks=8, yticks=5, xminor=5, yminor=5, $
         title=sub_title, ytitle=y_title, xtitle=x_title, $
         color=ncolor,charsize=0.7

   xyouts, pos[0]+0.018, pos[3]-0.1, "NO DATA POINTS IN CATEGORY", $
         charsize=0.6, color=ncolor, alignment=0, /normal

ENDELSE
end                     

;------------------------------------------------------------------------------


pro plot_scatter_rr_vs_tbb_by_raintype_ps, file, title, siteID, prz_in, gvz_in, $
                              Tbb_in, raintype_in, winsiz, UNITS=units, $
                              Y_TITLE=y_title, X_TITLE=x_title, SFCCAT=sfctype_in


; Set up color table
;
common colors, r_orig, g_orig, b_orig, r_curr, g_curr, b_curr ;load color table
loadct, 33, /SILENT
ncolor=!d.table_size-1
ncolor=255
red=bytarr(256) & green=bytarr(256) & blue=bytarr(256)
red=r_curr & green=g_curr & blue=b_curr
red(0)=255 & green(0)=255 & blue(0)=255
red(1)=215 & green(1)=215 & blue(1)=215  ;gray background
red(ncolor)=0 & green(ncolor)=0 & blue(ncolor)=0
IF N_ELEMENTS(sfctype_in) NE 0 THEN BEGIN
  ; set colors for sfc type: blue=ocean, green=land, red=coast
   red(251)=0 & green(251)=100 & blue(251)=255  ; ocean=251
   red(252)=50 & green(252)=200 & blue(252)=0   ; land=252
   red(253)=225 & green(253)=0 & blue(253)=50  ; coast=253
ENDIF

tvlct,red,green,blue

IF N_ELEMENTS(sfctype_in) NE 0 THEN BEGIN
  ; place colored text labels indicating sample color coding
  ; by underlying surface type
   xyouts, 0.5, 8.0/10., "OCEAN points", alignment=0.5, color=251, $
           /normal, charsize=1., Charthick=1.5
   xyouts, 0.5, 7.7/10., "LAND points", alignment=0.5, color=252, $
           /normal, charsize=1., Charthick=1.5
   xyouts, 0.5, 7.4/10., "COAST points", alignment=0.5, color=253, $
           /normal, charsize=1., Charthick=1.5
ENDIF

xyouts, 0.5, 9.3/10., title, alignment=0.5, color=ncolor, /normal, $
        charsize=1., Charthick=1.5

;bblevstr = ['  N/A ', 'Below BB', 'Within BB', 'Above BB']
Source_str = [' GMI ', siteID]
typestr = ['Any Type, ', 'Stratiform, ', 'Convective, ']

xs = 1.9/8.  &  ys = 1.9/10.   ; plot panel size
x0 = 0.5/8.  &  y0 = 1.5/10.    ; lower left plot panel position
x00 = 0.7/8.  &  y00 = 0.8/10. ; spacing between panels

pos = fltarr(4)

for i=0,1 do begin     ;row, count from bottom -- also, RR type

;  proxim = i+1     ; 1='Ocean', 2='Land', 3='Coast'
;  IF ( npts[i] GT 0 ) THEN BEGIN
;     idxthislev = idxBySfcType[i,0:npts[i]-1]
   IF i eq 0 THEN BEGIN
      prz = prz_in
      y_title = 'GMI rainate, mm/h'
   ENDIF ELSE BEGIN
      prz = gvz_in
      y_title = siteID + ' rainate, mm/h'
   ENDELSE
   gvz = Tbb_in
   raintype = raintype_in
   IF N_ELEMENTS(sfctype_in) NE 0 THEN BEGIN
      sfcColor = BYTARR( N_ELEMENTS(sfctype_in) )
     ; assign the surface type values 1,2,3 to their assigned
     ; color indices 251,252,253
      for isfc = 1,3 do begin
         idxcol=where(sfctype_in eq isfc, nsfc)
         if nsfc GT 0 then sfcColor[idxcol] = BYTE(isfc+250)
      endfor
   ENDIF

 ; don't indicate Ku-corrected for TMI plots
;  s2ku4sca = 0

  for j=0,2 do begin   ;column, count from left; also, strat/conv raintype index
    raincat = j ;+1   ; 1=Stratiform, 2=Convective
    x1=x0+j*(xs+x00)
    y1=y0+i*(ys+y00)  
    x2=x1+xs
    y2=y1+ys          
    pos[*] = [x1,y1,x2,y2]
    subtitle = typestr[raincat] +Source_str[i]
;    IF ( npts[i] GT 0 ) THEN BEGIN
      IF j EQ 0 THEN idxsub = WHERE( prz gt 0.0, nfound ) $
      ELSE idxsub = WHERE( prz gt 0.0 AND raintype EQ raincat, nfound )
      IF ( nfound GT 0 ) THEN BEGIN
         prsub = prz[idxsub] & gvsub = gvz[idxsub]
         IF N_ELEMENTS(sfcColor) NE 0 THEN sfcColorsub = sfcColor[idxsub]
         plot_rr_tbb_scat_ps, pos, subtitle, siteID, ncolor, charsz, gvsub, $
                    prsub, nfound, SFCCOLOR=sfcColorsub, UNITS=units, $
                    Y_TITLE=y_title, X_TITLE=x_title
      ENDIF ELSE BEGIN
         print, 'No points in rain category ', typestr[raincat] ;, Source_str[proxim]
         plot_rr_tbb_scat_ps, pos, subtitle, siteID, ncolor, charsz, $
                    UNITS=units, Y_TITLE=y_title, X_TITLE=x_title
      ENDELSE
;    ENDIF ELSE BEGIN
;      print, 'no points at BB proximity level'
;      plot_rr_tbb_scat, pos, subtitle, siteID, ncolor, charsz, $
;                 UNITS=units, Y_TITLE=y_title, X_TITLE=x_title
;    ENDELSE
  endfor

endfor

;SET_PLOT, orig_device
end
